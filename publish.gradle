apply plugin: 'maven-publish'

def nativeName = wpilibTools.platformMapper.wpilibClassifier;
def artifactGroupId = 'org.photonvision'
def baseArtifactId = "rknn_jni"

def jniType = project.findProperty("PublishType") ?: "";
println("Building with JNI publish type: " + jniType);

task nativeLibraryJar(type: Jar, dependsOn: copyNativeLibrary) {
    archiveClassifier = nativeName + jniType
    from outputsFolder
}
build.dependsOn nativeLibraryJar

publishing {
    repositories {
        maven {
            url ('https://maven.photonvision.org/' + (isDev ? 'snapshots' : 'releases'))
            credentials {
                username 'ghactions'
                password System.getenv("ARTIFACTORY_API_KEY")
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            groupId = artifactGroupId
            artifactId = "${baseArtifactId}-java"
            version = pubVersion;

            from components.java
        }
        mavenJNI(MavenPublication) {
            groupId = artifactGroupId
            artifactId = "${baseArtifactId}-jni"
            version = pubVersion;

            artifact nativeLibraryJar
        }
    }
}

tasks.withType(PublishToMavenRepository) {
    doFirst {
        println("Publishing rknn-jni to " + repository.url)
    }
}
